#define $console rec $console. \
  1=>2 \
  {^PutString: 1=>2<String>; $console, \
   ^PutInt:    1=>2<Int>;    $console, \
   ^GetString: 2=>1<String>; $console, \
   ^GetInt:    2=>1<Int>; $console, \
   ^End: Gend \
  } \
// HOSTHEADER("#include <iostream>");
(nu console: $console)
def ConsoleServer() =
  link(2,console,s,2);
  def ConsoleSession(s: $console@(2 of 2) ) =
    s[1]>>
    {^PutString: s[1]>>str;
                 HOST("std::cout << (", str, ").ToString() << std::endl;");
                 ConsoleSession(s),
     ^PutInt:    s[1]>>i;
                 HOST("std::cout << (", i, ").ToString() << std::endl;");
                 ConsoleSession(s),
     ^GetString: val:String="";
                 HOST("{ std::string _s; std::cin >> _s; ", val, "=libpi::StringValue(_s); }");
                 s[1]<<val;
                 ConsoleSession(s),
     ^GetInt:    val:Int=0;
                 HOST("{ long _l; std::cin >> _l; ", val, "=libpi::IntValue(_l); }");
                 s[1]<<val;
                 ConsoleSession(s),
     ^End:       ConsoleServer()
    }
  in ConsoleSession(s)
in ConsoleServer()
|
