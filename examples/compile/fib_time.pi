#include <console.pi>
#include <timer.pi>

#define $fib \
  2->1:Int; \
  1->2:Int; \
  $end;

global $fib fib(1 pure,2);
local Fib()
( global s = new fib(1 of 2);
  Fib(); |
  s[2] >> x;
  if x <=1
  then s[2] << 1;
  else s1 = new fib(2 of 2);
       s1[1] << x-1;
       s1[1] >> f1;
       s2=new fib(2 of 2);
       s2[1] << x-2;
       s2[1] >> f2;
       s[2] << f1+f2;
)
Fib(); |
global $fib timefib(1,2);
local TimeFib()
( global s = new timefib(1 of 2);
  TimeFib(); |
  s[2] >> n;
  t = new timer(2 of 2);
  f = new fib(2 of 2);
  f[1] << n;
  f[1] >> fn;
  s[2] << fn;
  t[1] << ^ReadSeconds;
  t[1] >> secs;
  t[1] << ^End;
  c = new console(2 of 2);
  c[1] << ^PutString << "used time"
       << ^PutInt << secs
       << ^End;
)
TimeFib(); |
c = new console(2 of 2);
c[1] << ^PutString << "What is n?"
     << ^GetInt;
c[1] >> v;
f = new timefib(2 of 2);
f[1] << v;
f[1] >> r;
c[1] << ^PutString << "Fib(n) is"
     << ^PutInt << r
     << ^End;
